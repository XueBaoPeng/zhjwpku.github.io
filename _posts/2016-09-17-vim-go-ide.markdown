---
layout: post
title: Vim Go IDE
date: 2016-09-17 22:00:00 +0800
tags:
- vim-go
- tagbar
- gotags
---

最近开始阅读Docker、Kubernetes源码，由于习惯了使用Vim，又太过慵懒而不愿学习其它IDE，因此笔者使用[vim-go][vim-go]、[tagbar][tagbar]打造了一个Go语言的集成开发环境。

**安装godep**

编辑~/.bash_profile或~/.bashrc，添加如下变量：
{% highlight shell %}
export GOPATH=$HOME/go
export PATH=$PATH:$GOPATH/bin
{% endhighlight %}

然后在命令行运行：
{% highlight shell %}
$go get -u github.com/tools/godep
$go get -u github.com/jteeuwen/go-bindata/go-bindata
{% endhighlight %}

**安装vim-go并配置vimrc**
{% highlight shell %}
$curl -fLo ~/.vim/autoload/plug.vim --create-dirs https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
$git clone https://github.com/fatih/vim-go.git ~/.vim/plugged/vim-go
$git clone https://github.com/fatih/vim-go-tutorial ~/vim-tutorial
$mv ~/vim-tutorial/vimrc ~/.vimrc
{% endhighlight %}

.vimrc中的内容如下：
{% highlight shell %}
" A sensible vimrc for Go development
"
" Please note that the following settings are some default that I used
" for years. However it might be not the case for you (and your
" environment). I highly encourage to change/adapt the vimrc to your own
" needs. Think of a vimrc as a garden that needs to be maintained and fostered
" throughout years. Keep it clean and useful - Fatih Arslan

call plug#begin()
Plug 'fatih/vim-go'
Plug 'fatih/molokai'
Plug 'AndrewRadev/splitjoin.vim'
Plug 'SirVer/ultisnips'
Plug 'ctrlpvim/ctrlp.vim'
call plug#end()

""""""""""""""""""""""
"      Settings      "
""""""""""""""""""""""
set nocompatible                " Enables us Vim specific features
filetype off                    " Reset filetype detection first ...
filetype plugin indent on       " ... and enable filetype detection
set ttyfast                     " Indicate fast terminal conn for faster redraw
set ttymouse=xterm2             " Indicate terminal type for mouse codes
set ttyscroll=3                 " Speedup scrolling
set laststatus=2                " Show status line always
set encoding=utf-8              " Set default encoding to UTF-8
set autoread                    " Automatically read changed files
set autoindent                  " Enabile Autoindent
set backspace=indent,eol,start  " Makes backspace key more powerful.
set incsearch                   " Shows the match while typing
set hlsearch                    " Highlight found searches
set noerrorbells                " No beeps
set number                      " Show line numbers
set showcmd                     " Show me what I'm typing
set noswapfile                  " Don't use swapfile
set nobackup                    " Don't create annoying backup files
set splitright                  " Vertical windows should be split to right
set splitbelow                  " Horizontal windows should split to bottom
set autowrite                   " Automatically save before :next, :make etc.
set hidden                      " Buffer should still exist if window is closed
set fileformats=unix,dos,mac    " Prefer Unix over Windows over OS 9 formats
set noshowmatch                 " Do not show matching brackets by flickering
set noshowmode                  " We show the mode with airline or lightline
set ignorecase                  " Search case insensitive...
set smartcase                   " ... but not it begins with upper case
set completeopt=menu,menuone    " Show popup menu, even if there is one entry
set pumheight=10                " Completion window max size
set nocursorcolumn              " Do not highlight column (speeds up highlighting)
set nocursorline                " Do not highlight cursor (speeds up highlighting)
set lazyredraw                  " Wait to redraw

" Enable to copy to clipboard for operations like yank, delete, change and put
" http://stackoverflow.com/questions/20186975/vim-mac-how-to-copy-to-clipboard-without-pbcopy
if has('unnamedplus')
  set clipboard^=unnamed
  set clipboard^=unnamedplus
endif

" This enables us to undo files even if you exit Vim.
if has('persistent_undo')
  set undofile
  set undodir=~/.config/vim/tmp/undo//
endif

" Colorscheme
syntax enable
set t_Co=256
let g:rehash256 = 1
let g:molokai_original = 1
" colorscheme molokai

""""""""""""""""""""""
"      Mappings      "
""""""""""""""""""""""

" Set leader shortcut to a comma ','. By default it's the backslash
let mapleader = ","

" Jump to next error with Ctrl-n and previous error with Ctrl-m. Close the
" quickfix window with <leader>a
map <C-n> :cnext<CR>
map <C-m> :cprevious<CR>
nnoremap <leader>a :cclose<CR>

" Visual linewise up and down by default (and use gj gk to go quicker)
noremap <Up> gk
noremap <Down> gj
noremap j gj
noremap k gk

" Search mappings: These will make it so that going to the next one in a
" search will center on the line it's found in.
nnoremap n nzzzv
nnoremap N Nzzzv

" Act like D and C
nnoremap Y y$

" Enter automatically into the files directory
autocmd BufEnter * silent! lcd %:p:h


"""""""""""""""""""""
"      Plugins      "
"""""""""""""""""""""

" vim-go
let g:go_fmt_command = "goimports"
let g:go_autodetect_gopath = 1
let g:go_list_type = "quickfix"

let g:go_highlight_types = 1
let g:go_highlight_fields = 1
let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_extra_types = 1
let g:go_highlight_generate_tags = 1

" Open :GoDeclsDir with ctrl-g
nmap <C-g> :GoDeclsDir<cr>
imap <C-g> <esc>:<C-u>GoDeclsDir<cr>


augroup go
  autocmd!

  " Show by default 4 spaces for a tab
  autocmd BufNewFile,BufRead *.go setlocal noexpandtab tabstop=4 shiftwidth=4

  " :GoBuild and :GoTestCompile
  autocmd FileType go nmap <leader>b :<C-u>call <SID>build_go_files()<CR>

  " :GoTest
  autocmd FileType go nmap <leader>t  <Plug>(go-test)

  " :GoRun
  autocmd FileType go nmap <leader>r  <Plug>(go-run)

  " :GoDoc
  autocmd FileType go nmap <Leader>d <Plug>(go-doc)

  " :GoCoverageToggle
  autocmd FileType go nmap <Leader>c <Plug>(go-coverage-toggle)

  " :GoInfo
  autocmd FileType go nmap <Leader>i <Plug>(go-info)

  " :GoMetaLinter
  autocmd FileType go nmap <Leader>l <Plug>(go-metalinter)

  " :GoDef but opens in a vertical split
  autocmd FileType go nmap <Leader>v <Plug>(go-def-vertical)
  " :GoDef but opens in a horizontal split
  autocmd FileType go nmap <Leader>s <Plug>(go-def-split)

  " :GoAlternate  commands :A, :AV, :AS and :AT
  autocmd Filetype go command! -bang A call go#alternate#Switch(<bang>0, 'edit')
  autocmd Filetype go command! -bang AV call go#alternate#Switch(<bang>0, 'vsplit')
  autocmd Filetype go command! -bang AS call go#alternate#Switch(<bang>0, 'split')
  autocmd Filetype go command! -bang AT call go#alternate#Switch(<bang>0, 'tabe')
augroup END

" build_go_files is a custom function that builds or compiles the test file.
" It calls :GoBuild if its a Go file, or :GoTestCompile if it's a test file
function! s:build_go_files()
  let l:file = expand('%')
  if l:file =~# '^\f\+_test\.go$'
    call go#cmd#Test(0, 1)
  elseif l:file =~# '^\f\+\.go$'
    call go#cmd#Build(0)
  endif
endfunction
{% endhighlight %}

接下来运行：
{% highlight shell %}
$vim -c "GoInstallBinaries" -c "qa"
{% endhighlight %}
这条命令会自动安装如下vim-go依赖包，用户也可以通过go get 'package_name'进行安装。

- github.com/nsf/gocode
- github.com/klauspost/asmfmt/cmd/asmfmt
- github.com/jstemmer/gotags
- github.com/kisielk/errcheck
- github.com/rogpeppe/godef
- github.com/golang/lint/golint
- golang.org/x/tools/cmd/gorename
- golang.org/x/tools/cmd/guru
- golang.org/x/tools/cmd/goimports
- github.com/alecthomas/gometalinter
- github.com/nsf/gocode
- github.com/fatih/motion
- github.com/zmb3/gogetdoc
- github.com/josharian/impl

详细的介绍和使用方法推荐阅读[vim-go-tutorial][vim-go-tutorial]。

至此，我们可以使用`Ctrl+]`和`Ctrl+t`来进行代码间的跳转。

**安装Tagbar**
{% highlight shell %}
$git clone https://github.com/majutsushi/tagbar
{% endhighlight %}
将autoload、doc、plugin、syntax文件夹放置在~/.vim文件夹下，如果~./vim中存在要mv的文件夹，则将操作细化到下一级文件夹，原则是保证原有的插件不变。

在~/.vimrc中添加如下内容：
{% highlight shell %}
nmap <F4> :TagbarToggle<CR>
{% endhighlight %}
这样就可以通过F4按钮来调出Tagbar。

[vim-go]: https://github.com/fatih/vim-go
[vim-go-tutorial]: https://github.com/fatih/vim-go-tutorial
[tagbar]: https://github.com/majutsushi/tagbar
